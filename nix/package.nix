{
  lib,
  stdenv,
  fetchurl,
  autoPatchelfHook,
  makeWrapper,
  wrapGAppsHook3,
  glib,
  gtk3,
  gdk-pixbuf,
  cairo,
  pango,
  atk,
  at-spi2-core,
  nss,
  nspr,
  dbus,
  cups,
  expat,
  zlib,
  xz,
  alsa-lib,
  libxkbcommon,
  libgbm,
  mesa,
  libGL,
  libdrm,
  fontconfig,
  freetype,
  systemdLibs,
  libx11,
  libxcb,
  libxcomposite,
  libxdamage,
  libxext,
  libxfixes,
  libxrandr,
  libxrender,
  libxcursor,
  libxi,
  libxtst,
  libxscrnsaver,
  libxshmfence,
}:

let
  pname = "coodi";
  version = "0.11.0";

  # Keep the formatting of these lines stable
  # (one `"<system>" = "sha256-...";` per line) so the workflow's sed can find
  # them.
  hashes = {
    "x86_64-linux" = "sha256-3lMBEomGObYLPV+W+haB4DIOVy0iW/ArxLmomnMt5rU=";
    "aarch64-linux" = "sha256-0vMiPPCsUiK/Tfpa8wHGV+KrH0yUyKUE2huDZ5IMZFI=";
  };

  arches = {
    "x86_64-linux" = "x86_64";
    "aarch64-linux" = "aarch64";
  };

  mkSource =
    system:
    fetchurl {
      url = "https://www.mubashirhassan.com/coodi/releases/download/v${version}/Coodi_${version}_linux-${arches.${system}}.tar.gz";
      hash = hashes.${system};
    };

  src =
    if hashes ? ${stdenv.hostPlatform.system} then
      mkSource stdenv.hostPlatform.system
    else
      throw "coodi: unsupported system ${stdenv.hostPlatform.system}";

  runtimeLibs = [
    glib
    gtk3
    gdk-pixbuf
    cairo
    pango
    atk
    at-spi2-core
    nss
    nspr
    dbus
    cups
    expat
    zlib
    xz
    alsa-lib
    libxkbcommon
    libgbm
    mesa
    libGL
    libdrm
    fontconfig
    freetype
    systemdLibs
    libx11
    libxcb
    libxcomposite
    libxdamage
    libxext
    libxfixes
    libxrandr
    libxrender
    libxcursor
    libxi
    libxtst
    libxscrnsaver
    libxshmfence
  ];
in
stdenv.mkDerivation {
  inherit pname version src;

  sourceRoot = "coodi.app";

  nativeBuildInputs = [
    autoPatchelfHook
    makeWrapper
    wrapGAppsHook3
  ];

  buildInputs = runtimeLibs;

  dontWrapGApps = true;

  # The upstream Linux launcher disables Vulkan, so this library is optional.
  autoPatchelfIgnoreMissingDeps = [ "libvulkan.so.1" ];

  installPhase = ''
    runHook preInstall

    mkdir -p $out/libexec $out/lib $out/share
    cp -r libexec/. $out/libexec/
    cp -r lib/. $out/lib/
    cp -r share/. $out/share/

    makeWrapper $out/libexec/coodi $out/bin/coodi \
      --add-flags "--ozone-platform=x11 --disable-vulkan --disable-features=Vulkan" \
      --prefix LD_LIBRARY_PATH : "$out/libexec:${lib.makeLibraryPath runtimeLibs}" \
      "''${gappsWrapperArgs[@]}"

    runHook postInstall
  '';

  meta = {
    description = "Coodi — a fast, extensible code editor (prebuilt Linux release)";
    homepage = "https://www.mubashirhassan.com/coodi";
    changelog = "https://www.mubashirhassan.com/coodi/releases/tag/v${version}";
    license = lib.licenses.agpl3Only;
    sourceProvenance = with lib.sourceTypes; [ binaryNativeCode ];
    platforms = [
      "x86_64-linux"
      "aarch64-linux"
    ];
    mainProgram = "coodi";
  };
}
